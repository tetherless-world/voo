{
    "summary": {
	"Ontology" : {{'select (count(?ontology) as ?ontologies) where {
	      ?ontology a owl:Ontology.
	      minus {
	        ?ontology prov:specializationOf ?o.
	      }
	      }' | query | map(attribute="ontologies")| first  }},
	"ConceptScheme" : {{'select (count(?ontology) as ?ontologies) where {
	      ?ontology a skos:ConceptScheme. filter(!isblank(?ontology))
	      minus {
	        ?ontology prov:specializationOf ?o.
	      }
	      }' | query | map(attribute="ontologies")| first  }},
	"import" : {{'
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
select 
  (count(distinct ?ontology) as ?source) 
  (count(distinct ?import) as ?target)
where {
  ?ontology owl:imports ?import.
}
' | query | first |tojson }},
	"namespaceUri" : {{'
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX vann: <http://purl.org/vocab/vann/>
prefix voo: <http://purl.org/whyis/voo/>  
select (count(distinct ?ontology) as ?ontologies) where {
  ?ontology a owl:Ontology.
  ?ontology vann:preferredNamespaceUri ?namespace.
}
' | query | map(attribute="ontologies")| first  }},
	"mireot" : {{ '
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX vann: <http://purl.org/vocab/vann/>
PREFIX sio: <http://semanticscience.org/resource/>
prefix voo: <http://purl.org/whyis/voo/>
SELECT
  (count(distinct ?ontology) as ?source)
  (count(distinct ?quoted) as ?target)
  (sum(distinct(?quote_count)) as ?uses) 
WHERE {
  ?ontology sio:hasAttribute [
    a voo:NamespaceUseCount;
    sio:inRelationTo ?namespace;
    sio:hasValue ?quote_count
  ].
  ?quoted vann:preferredNamespaceUri ?namespace.
  minus {?ontology owl:imports ?quoted}
} ' | query | first  | tojson }},
	"subset" : {{'
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX void: <http://rdfs.org/ns/void#>
SELECT
  (count(distinct ?ontology) as ?source)
  (count(distinct ?imports) as ?target)
  (count(distinct ?import) as ?subsets)
WHERE {
  ?ontology owl:imports ?import.
  ?imports void:subset ?import.
}
' | query | first | tojson}}
    },
    "sources" : {{'
prefix voo: <http://purl.org/whyis/voo/>

SELECT
  ?portal
  (sample(?homepage) as ?website)
  (count(distinct ?ontology) as ?listed)
  (count(distinct ?official_ontology) as ?loaded)
WHERE {
  ?portal a voo:OntologyPortal; foaf:homepage ?homepage.
  optional {
    ?ontology void:inDataset ?portal.
    optional {
      ?ontology dcat:distribution?/prov:specializationOf? ?official_ontology.
      ?official_ontology a owl:Ontology.
    }
  }
  minus {
    ?official_ontology prov:specializationOf ?o.
  }
} group by ?portal order by desc(?listed)
' | query | iter_labelize(key='portal') | tojson }},
    "top_imports" : {{'
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dcterms: <http://purl.org/dc/terms/>
prefix voo: <http://purl.org/whyis/voo/>

SELECT
  ?ontology
  (count(distinct ?importer) as ?count) 
WHERE {
  ?importer owl:imports/prov:specializationOf? ?ontology.
  minus {?ontology prov:specializationOf []}.
  minus {
    ?ontology dcterms:isPartOf ?collection.
    ?importer dcterms:isPartOf ?collection.
  }
  minus {
    ?ontology a voo:CoreOntology.
    #bind ("yes" as ?core)
  }
} group by ?ontology order by desc(?count) limit 10
' | query | iter_labelize(key='ontology') | tojson }},
    "top_subsets" : {{'
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX void: <http://rdfs.org/ns/void#>
PREFIX dcterms: <http://purl.org/dc/terms/>
SELECT 
  ?ontology
  (count(distinct ?subsetter) as ?count) 
WHERE {
  ?subsetter owl:imports ?subset.
  ?ontology void:subset ?subset.
  minus {
    ?ontology dcterms:isPartOf ?collection.
?subsetter dcterms:isPartOf ?collection.
  }
  filter(strstarts(str(?ontology), "http"))
} 
group by ?ontology 
order by desc(?count)
limit 10
' | query | iter_labelize(key='ontology') | tojson }},
    "top_mireots" : {{'
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX dcterms: <http://purl.org/dc/terms/>
prefix voo: <http://purl.org/whyis/voo/>

SELECT
  ?ontology 
  (count(distinct ?importer) as ?indegree) 
WHERE {
  ?importer owl:imports/prov:specializationOf? ?ontology.
  minus {?ontology prov:specializationOf []}.
  minus {
    ?ontology dcterms:isPartOf ?collection.
?importer dcterms:isPartOf ?collection.
  }
  minus {
    ?ontology a voo:CoreOntology.
  }
}
group by ?ontology
order by desc(?indegree)
limit 10
' | query | iter_labelize(key='ontology') | tojson }}

}
